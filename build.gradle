plugins {
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
}

apply plugin: 'maven-publish' // for uploading to a maven repo

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "LibBlockAttributes"
version = "0.3.0"

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:19w13a"
    mappings "net.fabricmc:yarn:19w13a.2"
    modCompile "net.fabricmc:fabric-loader:0.3.7.109"

    //Fabric api
    modCompile "net.fabricmc:fabric:0.2.6.116"
    compile "com.google.code.findbugs:jsr305:3.0.1"

    // Silk API
    modCompile "io.github.prospector.silk:SilkAPI:1.2.3-38"

    // Misc
    compile "com.google.code.findbugs:jsr305:3.0.1"
    testCompile "junit:junit:4.12"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

compileJava {
    options.compilerArgs << "-Xmaxerrs" << "2000"
    options.compilerArgs << "-Xmaxwarns" << "2"
    options.compilerArgs << "-Xlint:all"
    options.compilerArgs << "-Xdiags:verbose"
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "$projectDir/build/maven"
        }
    }
    publications {
       publishRemapJar(MavenPublication) {
            groupId "alexiil.mc.lib"
            artifactId "libblockattributes"
            version project.version

            artifact (jar) {
                builtBy remapJar
            }
        }
        publishRemapSrcJar(MavenPublication) {
            groupId "alexiil.mc.lib"
            artifactId "libblockattributes"
            version project.version

            artifact (sourcesJar) {
                classifier "sources"
                builtBy remapSourcesJar
            }
        }
    }
}
